#region Prolog
#-----------------------------------------------------------------------------------------------------------------------------------------------------#
# PROCESSUS DESCRIPTION
#
# Date :   29/01/2025
# Autor :  Iena Consulting
#
# Action : This process is for exporting "Pers Cost" file for the Budget phase
#-----------------------------------------------------------------------------------------------------------------------------------------------------#

#====================================#
#         Define Constants           #
#====================================#

#---- Global Variables ----#
c_Process=getProcessName;
c_TimeStamp = TimSt( Now, '\Y\m\d\h\i\s' );

n_Errors = 0;
s_Message = '';
n_DataCount = 0;
nHeaders = 1;

c_subsetPrefix = TimSt( Now, '\h\i\s_' ) | NumberToString( INT( RAND() * 10000000));

c_Date = TimSt( Now, '\d-\m-\Y \h:\i:\s' );

#---- Cubes ----#
c_cub_Source =  'Budget Reporting Consultation';
c_cub_Source_AIE =  'Budget Information Employee';
c_cubAttrMonth='}ElementAttributes_Month';
c_cubAttrBudgetEmployee='}ElementAttributes_Budget Employee';
c_cubAttrGroupStructure = '}ElementAttributes_Group Structure';
c_cubAttrLCC='}ElementAttributes_Local Cost Center';
c_cubAttrBanding='}ElementAttributes_Banding';
c_cubAttrOrganizationMicrofunction='}ElementAttributes_Organization Microfunction';
c_cubAttrCategory = '}ElementAttributes_Category';
c_cubAttrStore = '}ElementAttributes_Store';
c_cubAttrVersion = '}ElementAttributes_Version';
c_cubAttrReportingPhase = '}ElementAttributes_Reporting Phase';


#----- DImensions-----#
D_dim_Year = 'Budget Year';
D_dim_Phase = 'Phase';
D_dim_Reporting_Phase = 'Reporting Phase';
D_dim_BudgetVersion = 'Version';
D_dim_Month =  'Month';
D_dim_GroupStructure = 'Group Structure';
D_dim_Organization_Microfunction = 'Organization Microfunction';
D_dim_Local_Cost_Center = 'Local Cost Center';
D_dim_Store = 'Store';
D_dim_Category = 'Category';
D_dim_Banding = 'Banding';
D_dim_Budget_Employee = 'Budget Employee';
D_dim_Currency = 'Reporting Currency';
D_dim_ReportingExchangesRates = 'Reporting Exchange Rates';
D_dim_Budget_Reporting_Consultation_Measures = 'Budget Reporting Consultation Measures';
D_dim_Budget_Reporting_Consultation_Value='Budget Reporting Consultation Value';



#----- Subset -----#

c_SourceSubset = 'z_Source' | c_Process | '_' | c_subsetPrefix ;
c_SourceViewY = 'z_Source' | c_Process | '_' | c_subsetPrefix ;


#====================================#
#         Control Parameter          #
#====================================#
SetInputCharacterSet ('TM1CS_UTF8');

n_ErrorsProlog = 0;


#### Year année appartenant à la dimension YEAR et de niveau 0

# # IF(DIMIX(D_dim_Year, p_Year) = 0 % ELLEV(D_dim_Year,p_Year)<>0);
# IF(DIMIX(D_dim_Year, p_Year) = 0 );
#     n_ErrorsProlog = 1;
#     s_MessageProlog = 'Invalid Year Parameter :'| p_Year |' doesn''t exist in Year Dimension';
#     DataSourceType='NULL';
#     itemreject(s_MessageProlog);
# ENDIF;

### Month

IF(DIMIX(D_dim_Month , p_Month) = 0  &  p_Month @<> 'All');       
    n_ErrorsProlog = 1;
    s_MessageProlog = 'Invalid Month Parameter : doesn''t exist in Month Dimension';
    DataSourceType='NULL';
    itemreject(s_MessageProlog);
ENDIF;


### Entity

IF(DIMIX(D_dim_GroupStructure , P_Entity) = 0  & p_Entity @<> 'All' );       
    n_ErrorsProlog = 1;
    s_MessageProlog = 'Invalid Entity Parameter : doesn''t exist in Group Structure Dimension';
    DataSourceType='NULL';
    itemreject(s_MessageProlog);
ENDIF;

# ## Phase
# If( DIMIX(D_dim_Phase, p_Phase) = 0 % ELLEV(D_dim_Phase,p_Phase)<>0 );
#   n_ErrorsProlog = 1;
#   s_MessageProlog = 'Invalid phase specified : doesn''t exist in Phase Dimension ';
#   DataSourceType = 'NULL';
#   ItemReject(  s_MessageProlog);
# EndIf;

## Concatination Phase/Year

V_Concat_Phase_Year = p_Phase|' '|p_Year;
V_Concat_Phase_YearID = DimensionElementPrincipalName( D_dim_Reporting_Phase, V_Concat_Phase_Year );
Subst_Year = SUBST( V_Concat_Phase_YearID, 1, 4 );

## Reporting Phase
If(DIMIX(D_dim_Reporting_Phase, V_Concat_Phase_Year) = 0  % ELLEV(D_dim_Reporting_Phase,V_Concat_Phase_Year)<>0);
  n_ErrorsProlog = 1;
  s_MessageProlog = 'Invalid phase specified : '|V_Concat_Phase_Year|' doesn''t exist in Reporting  Phase Dimension ';
  DataSourceType = 'NULL';
  ItemReject(  s_MessageProlog);
EndIf;

## Version
If( DIMIX(D_dim_BudgetVersion, p_Version) = 0 % ELLEV(D_dim_BudgetVersion,p_Version)<>0 );
  n_ErrorsProlog = 1;
  s_MessageProlog = 'Invalid Version specified : doesn''t exist in Version Dimension';
  DataSourceType = 'NULL';
  ItemReject(  s_MessageProlog );
EndIf;

viewCreate(c_cub_Source, c_SourceViewY,1);

#=========================================#
#       Creation of the source view       #
#=========================================#

#--------------------------#
# Reporting Phase subset   #
#-------------------------#

subsetCreate(D_dim_Reporting_Phase, c_SourceSubset, 1);
subsetElementInsert(D_dim_Reporting_Phase, c_SourceSubset, V_Concat_Phase_Year, 1);
viewSubsetAssign(c_cub_source, c_SourceViewY, D_dim_Reporting_Phase, c_SourceSubset);


#--------------------------#
# Version subset   #
#-------------------------#

subsetCreate(D_dim_BudgetVersion, c_SourceSubset, 1);
subsetElementInsert(D_dim_BudgetVersion, c_SourceSubset, p_Version, 1);
viewSubsetAssign(c_cub_Source, c_SourceViewY, D_dim_BudgetVersion, c_SourceSubset);


#--------------------------#
# Month subset   #
#-------------------------#

If (P_Month @= 'All');
    P_Month = 'Total Year';
    subsetCreate(D_dim_Month, c_SourceSubset,1);
    subsetElementInsert(D_dim_Month, c_SourceSubset, 'Jan YTD', 1);
    subsetElementInsert(D_dim_Month, c_SourceSubset, 'Feb YTD', 1);
    subsetElementInsert(D_dim_Month, c_SourceSubset, 'Mar YTD', 1);
    subsetElementInsert(D_dim_Month, c_SourceSubset, 'Apr YTD', 1);
    subsetElementInsert(D_dim_Month, c_SourceSubset, 'May YTD', 1);
    subsetElementInsert(D_dim_Month, c_SourceSubset, 'Jun YTD', 1);
    subsetElementInsert(D_dim_Month, c_SourceSubset, 'Jul YTD', 1);
    subsetElementInsert(D_dim_Month, c_SourceSubset, 'Aug YTD', 1);
    subsetElementInsert(D_dim_Month, c_SourceSubset, 'Sep YTD', 1);
    subsetElementInsert(D_dim_Month, c_SourceSubset, 'Oct YTD', 1);
    subsetElementInsert(D_dim_Month, c_SourceSubset, 'Nov YTD', 1);
    subsetElementInsert(D_dim_Month, c_SourceSubset, 'Dec YTD', 1);																			   
    viewSubsetAssign(c_cub_Source, c_SourceViewY, D_dim_Month, c_SourceSubset);
else;
    subsetCreate(D_dim_Month , c_SourceSubset,1);
    subsetElementInsert(D_dim_Month , c_SourceSubset, P_Month , 1);
    viewSubsetAssign(c_cub_Source, c_SourceViewY,D_dim_Month , c_SourceSubset);
EndIf;


#--------------------------#
# Group Structure  subset #
#-------------------------#
If (P_Entity @= 'All');
    P_Entity = 'Total Entity';
EndIf;
s_MDXRequest = '{TM1FILTERBYLEVEL(DESCENDANTS({[ Group Structure ].['| P_Entity |']}) , 0)}';
SubsetCreateByMDX(c_SourceSubset,s_MDXRequest, 1);
SubsetElementInsert(D_dim_GroupStructure, c_SourceSubset, DIMNM(D_dim_GroupStructure, 1), 1);
SubsetElementDelete(D_dim_GroupStructure, c_SourceSubset , 1 );
viewSubsetAssign(c_cub_Source, c_SourceViewY, D_dim_GroupStructure, c_SourceSubset);

#--------------------------#
# # Organization Microfunction subset
#-------------------------#

s_MDXRequest = '{TM1FILTERBYLEVEL(DESCENDANTS({[Organization Microfunction].[Total Organizational Micro Function]}) , 0)}';
SubsetCreateByMDX(c_SourceSubset,s_MDXRequest, 1);
SubsetElementInsert(D_dim_Organization_Microfunction, c_SourceSubset, DIMNM(D_dim_Organization_Microfunction, 1), 1);
SubsetElementDelete(D_dim_Organization_Microfunction, c_SourceSubset , 1 );
viewSubsetAssign(c_cub_Source, c_SourceViewY, D_dim_Organization_Microfunction, c_SourceSubset);


#--------------------------#
# # LCC subset
#-------------------------#

s_MDXRequest = '{TM1FILTERBYLEVEL(DESCENDANTS({[Local Cost Center].[Total Local Cost Center]}) , 0)}';
SubsetCreateByMDX(c_SourceSubset,s_MDXRequest, 1);
SubsetElementInsert(D_dim_Local_Cost_Center, c_SourceSubset, DIMNM(D_dim_Local_Cost_Center, 1), 1);
SubsetElementDelete(D_dim_Local_Cost_Center, c_SourceSubset , 1 );
viewSubsetAssign(c_cub_Source, c_SourceViewY, D_dim_Local_Cost_Center, c_SourceSubset);

#--------------------------#
# # STORE subset
#-------------------------#
s_MDXRequest = '{TM1FILTERBYLEVEL(DESCENDANTS({[Store].[Total Store and Non Store]}) , 0)}';
SubsetCreateByMDX(c_SourceSubset,s_MDXRequest, 1);
SubsetElementInsert(D_dim_Store, c_SourceSubset, DIMNM(D_dim_Store, 1), 1);
SubsetElementDelete(D_dim_Store, c_SourceSubset , 1 );
viewSubsetAssign(c_cub_Source, c_SourceViewY, D_dim_Store, c_SourceSubset);

#--------------------------#
# # Category subset
#-------------------------#
s_MDXRequest = '{TM1FILTERBYLEVEL(DESCENDANTS({[Category].[Total Selling/Non Selling]}) , 0)}';
SubsetCreateByMDX(c_SourceSubset,s_MDXRequest, 1);
SubsetElementInsert(D_dim_Category, c_SourceSubset, DIMNM(D_dim_Category, 1), 1);
SubsetElementDelete(D_dim_Category, c_SourceSubset , 1 );
viewSubsetAssign(c_cub_Source, c_SourceViewY, D_dim_Category, c_SourceSubset);

#--------------------------#
# # Banding subset
#-------------------------#
s_MDXRequest = '{TM1FILTERBYLEVEL(DESCENDANTS({[Banding].[All Bandings]}) , 0)}';
SubsetCreateByMDX(c_SourceSubset,s_MDXRequest, 1);
SubsetElementInsert(D_dim_Banding, c_SourceSubset, DIMNM(D_dim_Banding, 1), 1);
SubsetElementDelete(D_dim_Banding, c_SourceSubset , 1 );
viewSubsetAssign(c_cub_Source, c_SourceViewY, D_dim_Banding, c_SourceSubset);


#--------------------------#
# Budget Employee  subset # 
#-------------------------#

s_MDXRequest = '{TM1FILTERBYLEVEL(DESCENDANTS({[Budget Employee].[Total Employee]}) , 0)}';
SubsetCreateByMDX(c_SourceSubset,s_MDXRequest, 1);
SubsetElementInsert(D_dim_Budget_Employee, c_SourceSubset, DIMNM(D_dim_Budget_Employee, 1), 1);
SubsetElementDelete(D_dim_Budget_Employee, c_SourceSubset , 1 );
viewSubsetAssign(c_cub_Source, c_SourceViewY, D_dim_Budget_Employee, c_SourceSubset);


#--------------------------#
# # Currency :   
#-------------------------#
subsetCreate(D_dim_Currency , c_SourceSubset,1);
subsetElementInsert(D_dim_Currency , c_SourceSubset, 'Local Currency' , 1);
subsetElementInsert(D_dim_Currency , c_SourceSubset, 'EUR' , 1);
viewSubsetAssign(c_cub_Source, c_SourceViewY,D_dim_Currency, c_SourceSubset);	

#--------------------------#
# # Reporting Exchange Rates :   
#-------------------------#
subsetCreate(D_dim_ReportingExchangesRates , c_SourceSubset,1);
subsetElementInsert(D_dim_ReportingExchangesRates , c_SourceSubset, 'Current phase Rate (Reported currency)' , 1);
viewSubsetAssign(c_cub_Source, c_SourceViewY,D_dim_ReportingExchangesRates, c_SourceSubset);

#--------------------------#
# Budget reporting consultation MEASURES   #
#--------------------------#
s_MDXRequest = '{TM1FILTERBYLEVEL(DESCENDANTS({[Budget Reporting Consultation Measures].[HRCOA00001]}) , 0)}';

SubsetCreateByMDX(c_SourceSubset,s_MDXRequest, 1);
SubsetElementInsert(D_dim_Budget_Reporting_Consultation_Measures, c_SourceSubset, DIMNM(D_dim_Budget_Reporting_Consultation_Measures, 1), 1);
SubsetElementDelete(D_dim_Budget_Reporting_Consultation_Measures, c_SourceSubset , 1 );
viewSubsetAssign(c_cub_Source, c_SourceViewY, D_dim_Budget_Reporting_Consultation_Measures, c_SourceSubset);

#--------------------------#
# # Budget reporting consultation value:   
#-------------------------#
subsetCreate(D_dim_Budget_Reporting_Consultation_Value , c_SourceSubset,1);
subsetElementInsert(D_dim_Budget_Reporting_Consultation_Value , c_SourceSubset, 'Total Value' , 1);
viewSubsetAssign(c_cub_Source, c_SourceViewY,D_dim_Budget_Reporting_Consultation_Value, c_SourceSubset);

#========================================#
#   reset of the Source views             #
#========================================#

ViewExtractSkipCalcsSet (C_cub_Source, c_SourceViewY, 0);
ViewExtractSkipRuleValuesSet (C_cub_Source, c_SourceViewY, 0);
ViewExtractSkipZeroesSet (C_cub_Source, c_SourceViewY, 1);

DatasourceCubeView=c_SourceViewY ;


#------------------------------------------------------------------------------------------------------

If (P_Month @= 'Total Year' & P_Entity @= 'Total Entity' );
    sFileName = 'Personnel_Cost'|'_'| Subst_Year|'_' | p_Phase|'_'| p_Version|'_YTD'|'.csv';
ELSEIF (P_Month @= 'Total Year' & P_Entity @<> 'Total Entity' );
    sFileName = 'Personnel_Cost'|'_'| Subst_Year|'_' | p_Entity|'_'| p_Phase|'_'| p_Version|'_YTD'|'.csv';
ELSEIF (P_Entity @= 'Total Entity' & P_Month @<> 'Total Year' );
    sFileName = 'Personnel_Cost'|'_'| Subst_Year|'_' | P_Month|'_'| p_Phase|'_'| p_Version|'.csv';  
ELSEIF (P_Entity @<> 'Total Entity' & P_Month @<> 'Total Year' );
    sFileName = 'Personnel_Cost'|'_'| Subst_Year|'_' | P_Month|'_'| p_Entity|'_'| p_Phase|'_'| p_Version|'.csv'; 
ENDIF;



IF(Subst(pFilePath, LONG(pFilepath), 1) @<> '\');
    pFilePath = pFilePath | '\';
ENDIF;

#Set output options:
DatasourceASCIIDelimiter=';';
DatasourceASCIIQuoteCharacter= '';
# DatasourceASCIIDecimalSeparator='.';
DatasourceASCIIThousandSeparator='';


#First Record - set once here so that title record can be added to the output
bFirstRec = 'TRUE';
#endregion
#region Data
#-------------MAPPING Workday ID------------# 
V_WorkdayID=CellGetS(c_cubAttrBudgetEmployee , V_Budget_Employee , 'Workday ID' );
If (V_WorkdayID @= '');
        V_WorkdayID=V_Budget_Employee;
ENDIF;

#-------------MAPPING Employee ID------------# 
V_EmployeeID=CellGetS(c_cubAttrBudgetEmployee , V_Budget_Employee , 'Employee ID' );
If (V_EmployeeID @= '');
        V_EmployeeID=V_Budget_Employee;
ENDIF;

#------------Mapping Entity HFM Code ----------------#
V_HFM_Code = CellGetS( c_cubAttrGroupStructure , V_GS,'HFM Code' );
If (V_HFM_Code @= '');
        V_HFM_Code=V_GS;
ENDIF;

#------------Mapping Entity HFM Hierachy ----------------#
V_HFM_Hierarchy = CellGetS( c_cubAttrGroupStructure , V_GS,'HFM Hierarchy' );
If (V_HFM_Hierarchy @= '');
        V_HFM_Hierarchy=V_GS;
ENDIF;

#------------Mapping LCC ----------------#
V_LCC_Code = CellGetS( c_cubAttrLCC, V_LCC,'Code' );
If (V_LCC_Code @= '');
        V_LCC_Code=V_LCC;
ENDIF;

#------------Mapping CATEGORY ----------------#
V_Category_Code = CellGetS( c_cubAttrCategory , V_Category,'Code' );
If (V_Category_Code @= '');
        V_Category_Code=V_Category;
ENDIF;

#------------Mapping Banding ----------------#
V_Banding_Code = CellGetS( c_cubAttrBanding, V_Banding, 'Workday Code' );
If (V_Banding_Code @= '');
        V_Banding_Code=V_Banding;
ENDIF;

#------------Mapping Store ----------------#
V_Store_Code = CellGetS( c_cubAttrStore , V_Store,'Code' );
If (V_Store_Code @= '');
        V_Store_Code=V_Store;
ENDIF;



#------------------------------------TEXT OUTPUT---------------------------------------------------#
#'Category','Gender
SetOutputCharacterSet( pFilepath | sFileName, 'TM1CS_UTF8' );

IF( nHeaders = 1);
    TextOutput(pFilepath | sFileName, 'Phase','Year', 'Month', 'HRA Code', 'Workday ID', 'Employee ID', 'Budget Entity Code', 'Budget HFM Code','Budget HFM Hierarchy',
               'Local Cost Center','Organization Microfunction','Reporting Exchange Rates','Category','Compensation Grade','Store','Reporting Currency','HR Account','Value');
    nHeaders = 0;
ENDIF;

V1=NumberToStringEx(Value, '0.##################', '.', '');

TextOutput( pFilepath | sFileName, p_Phase,Subst_Year, V_Month, V_Budget_Employee,V_WorkdayID, V_EmployeeID, V_GS, V_HFM_Code, V_HFM_Hierarchy, V_LCC_Code, V_Org_Mif,
        V_Exchange_Rate,V_Category_Code,V_Banding_Code,V_Store_Code,V_Reporting_Currency,V_Measures,V1);
#endregion